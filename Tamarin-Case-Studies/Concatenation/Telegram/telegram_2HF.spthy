theory Telegram

/*

As described in Fig 57 of


https://mtpsym.github.io/paper.pdf


As operation N = p*q is simply meant to model a proof of work, we simply model it as N=p + q, multiset

RSA is asymmetric encryption, AES is symmetric.

// The attack on auth is simply sending col  instead of hnprime in the last C step, dummy attack
// to explore if the kdf with h(x||y) breaks secrecy in the more fine grained models

==============================================================================
summary of summaries:

analyzed: telegram_2HF.spthy

  hon_ex (exists-trace): verified (16 steps)
  dis_ex (exists-trace): verified (14 steps)
  auth (all-traces): verified (7 steps)
  secBoth (all-traces): verified (323 steps)
  secC (all-traces): verified (69 steps)

==============================================================================

real	0m37.250s
user	2m54.292s
sys	1m2.923s
─( 16:59:44 )─< ~ >────────────────────────────────────────────────────────────────────────────[ 0 ]─
c01chja@Colossus02 $ time tamarin-prover telegram_2HF.spthy +RTS -N10 -RTS --prove -D=collapse



==============================================================================
summary of summaries:

analyzed: telegram_2HF.spthy

  hon_ex (exists-trace): verified (16 steps)
  dis_ex (exists-trace): verified (12 steps)
  auth (all-traces): verified (7 steps)
  secBoth (all-traces): verified (295 steps)
  secC (all-traces): verified (69 steps)

==============================================================================

real	0m27.132s
user	2m10.373s
sys	0m45.768s
─( 17:00:26 )─< ~ >────────────────────────────────────────────────────────────────────────────[ 0 ]─
c01chja@Colossus02 $ time tamarin-prover telegram_2HF.spthy +RTS -N10 -RTS --prove -D=collapseh


==============================================================================
summary of summaries:

analyzed: telegram_2HF.spthy

  hon_ex (exists-trace): verified (12 steps)
  dis_ex (exists-trace): verified (12 steps)
  auth (all-traces): falsified - found trace (7 steps)
  secBoth (all-traces): verified (879 steps)
  secC (all-traces): falsified - found trace (8 steps)

==============================================================================

real	1m23.543s
user	6m25.911s
sys	2m37.008s
─( 17:02:00 )─< ~ >────────────────────────────────────────────────────────────────────────────[ 0 ]─
c01chja@Colossus02 $ time tamarin-prover telegram_2HF.spthy +RTS -N10 -RTS --prove -D=collapsehdeux


*/
begin

builtins: symmetric-encryption, asymmetric-encryption, hashing, diffie-hellman, multiset
functions: h2/1
// kdf is apparently h(x||y)

rule initRSAkeys:
[Fr(~skS)]--[Single(), Key(~skS)]->[S1(~skS), !C1(pk(~skS)), Out(pk(~skS))]


#ifdef single

restriction single:
 "All #i #j. Single()@i & Single()@j ==> #i = #j"

#endif

#ifdef collapseh

functions: col/0
equations: h(x)=col

#endif

#ifdef collapsehdeux

functions: col/0
equations: h2(x)=col

#endif


rule C_sendn:
[!C1(pk(~skS)),Fr(~n)]-->[Out(~n),C2(pk(~skS),~n)]

rule S_sendN:
  let N=~p + ~q in
[S1(skS),In(n),Fr(~s),Fr(~p),Fr(~q)]-->[Out(<n,~s,N>),S2(skS,~s,n,N)]


rule C_RSA:
  let hr = h(<p+q,p,q,~n,s,~nprime>)
      rsa = aenc(<hr,p+q,p,q,~n,s,~nprime> ,pk(~skS)) in// in Fig 57, pr is padding
[C2(pk(~skS),~n), In(<~n,s,p+q>), Fr(~nprime)]--[Key(~nprime)]->[Out(<~n,s,p,q,rsa>), C3(pk(~skS),~n,s,p,q,~nprime)]


rule S_AES:
  let key =h2(<nprime,~s>)
      hr = h( <bigNprime,p,q,n,~s,nprime> )
      hs = h(<n,~s, 'g', 'g'^(~a), ~ts>)
in
[ S2(skS,~s,n,bigN), In(<n,~s,p,q, aenc( <hr,bigNprime, p,q,n,~s,nprime> ,pk(skS))>)
, Fr(~ts) // a timestamp on the server
, Fr(~a) // DH exponent
]--[
Eq(p+q,bigN) // remark that paper says no check on bigN and bigNprime
]->[Out(<n,~s, senc( <hs,n,~s, 'g',  'g'^(~a), ~ts>  , key)>),
S3(skS,~s,n,bigN, nprime, ~a)


]  // we remove p', used to agree on a group, and fix the group to 'g'



rule C_AES:
  let key =h2(<~nprime,s>)
      hs = h(<~n,s, 'g', gA, ts>)
      hc = h(<~n,s,'retryid', 'g','g'^~b>)
  in
 [C3(pk(~skS),~n,s,p,q,~nprime) , In(<~n,s, senc( <hs,~n,s, 'g', gA, ts >, key)>),Fr(~b) ]-->[
Out( <~n,s , senc( < hc,~n,s, 'retryid', 'g'^~b>, key) >),
C4(pk(~skS),~n,s,p,q,~nprime, gA, ~b)

]


rule S_AN:
  let key =h2(<nprime,~s>)
      hc = h(<n,~s,'retryid', 'g',gB>)
      hnprime = h2( <nprime,h2( gB^~a)> )
  in
[S3(skS,~s,n,bigN, nprime, ~a),
In( <n, ~s, senc(< hc, n,~s, 'retryid', gB>,key)>)
]--[KeyS(gB^~a)]->[ Out( <n,~s, hnprime> ) ]


rule C_final:
  let key =h2(<~nprime,s>)
      hnprime = h2( <~nprime,h2( gA^~b)> )
  in
[C4(pk(~skS),~n,s,p,q,~nprime, gA, ~b), In(<~n,s,hnprime>)]--[KeyC( gA^~b)]->[]


restriction eq:
"All x y #i. Eq(x,y)@i ==> x=y"


lemma secserv[sources]:
"not (Ex key #i #j. Key(key)@i & KU(key)@j)"

lemma hon_ex:
 exists-trace
 "Ex key #i #j. KeyC(key)@i & KeyS(key)@j"


lemma dis_ex: // attacker can communicate with server
 exists-trace
 "Ex key #i #j. K(key)@i & KeyS(key)@j"


lemma auth: // C can only communicate with server
"All key #i. KeyC(key)@i ==> Ex #j. KeyS(key)@j"

lemma secBoth: //share key is secret
 "not(
 Ex key #i #j #l.
   KeyC(key)@i &
   KeyS(key)@j &
   K(key)@l
)"



lemma secC: //share key is secret
 "not(
 Ex key #i #l.
   KeyC(key)@i &
   K(key)@l
)"



end